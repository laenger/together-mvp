import groovyx.GroovyAndroidPlugin

buildscript {
    repositories {
        jcenter()
    }
    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.1'
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'org.codehaus.groovy:groovy-android-gradle-plugin:1.1.0'
    }
}

ext {
    runningOnJenkins = System.getenv('JENKINS_URL') != null
    nexusReleasesRepository = getEnvOrProperty('NEXUS_RELEASES_REPOSITORY')
    nexusSnapshotsRepository = getEnvOrProperty('NEXUS_SNAPSHOTS_REPOSITORY')
    nexusUsername = getEnvOrProperty('NEXUS_USERNAME')
    nexusPassword = getEnvOrProperty('NEXUS_PASSWORD')

    compileSdkVersion = 24
    buildToolsVersion = '24.0.3'
    targetSdkVersion = 24

    supportVersion = '24.2.1'
    daggerVersion = '2.6.1'
    retrofitVersion = '2.1.0'
    butterknifeVersion = '8.4.0'
    leakcanaryVersion = '1.5'
}

allprojects {
    repositories {
        jcenter()
        maven { url "https://raw.github.com/laenger/maven-releases/master/releases" }
        maven { url "https://raw.github.com/laenger/maven-releases/master/snapshots" }
    }

    plugins.withType(GroovyAndroidPlugin) {
        dependencies {
            testCompile 'org.codehaus.groovy:groovy:2.4.7:grooid'
            testCompile 'org.spockframework:spock-core:1.0-groovy-2.4'
            testCompile 'cglib:cglib-nodep:3.2.4'
            testCompile 'org.objenesis:objenesis:2.4'
        }
    }
}

task clean(type: Delete) {
    delete rootProject.buildDir
}

def getEnvOrProperty(String name) {
    return System.getenv(name) ?: project.hasProperty(name) ? project[name] : '';
}
