apply plugin: 'com.android.library'

description = 'Together Android MVP Library'
group = 'biz.laenger.android'

android {
    compileSdkVersion rootProject.compileSdkVersion
    buildToolsVersion rootProject.buildToolsVersion
    defaultPublishConfig 'debug'
    publishNonDefault true

    defaultConfig {
        minSdkVersion 15
        targetSdkVersion rootProject.targetSdkVersion
        versionName '1.0.0'
    }

    buildTypes {
        release {
        }
        debug {
        }
    }

    libraryVariants.all { variant ->
        variant.outputs.each { output ->
            def outputFile = output.outputFile
            if (outputFile != null && outputFile.name.endsWith('.aar')) {
                def fileName = "${project.name}_${android.defaultConfig.versionName}_${buildVersionSuffix()}-${variant.buildType.name}.aar"
                output.outputFile = new File(outputFile.parent, fileName)
            }
        }
    }

    lintOptions {
        abortOnError false
    }
}

dependencies {
    compile "com.android.support:appcompat-v7:${supportVersion}"
    compile 'biz.laenger.android:tomorrow:1.0.3-SNAPSHOT'
}

def buildVersionSuffix() {
    if (runningOnJenkins) {
        return "${System.getenv('BUILD_NUMBER').trim()}_${System.getenv('GIT_COMMIT').subSequence(0, 6).trim()}"
    }
    return 'local'
}

apply from: 'maven.gradle'
